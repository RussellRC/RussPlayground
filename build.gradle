buildscript {
    ext {
        springBootVersion = '2.6.1'
        jerseyVersion = '2.35'
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath ('io.spring.gradle:dependency-management-plugin:1.0.11.RELEASE') {
            exclude module: 'log4j'
        }
        classpath ("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

allprojects {
    project.group = 'org.russell'
    project.version = '1.0-SNAPSHOT'

    ext['jackson.version'] = '2.11.4'
    ext['jackson.version'] = '2.11.4'

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 11
    targetCompatibility = 11

    repositories {
        mavenCentral()
    }

    sourceSets {
        integrationTest {
            java.srcDirs = ['src/integration-test/java']
            resources.srcDirs = ['src/integration-test/resources']
            compileClasspath += main.output + test.compileClasspath
            runtimeClasspath += main.output + test.runtimeClasspath
        }
    }

    configurations {
        integrationTestImplementation {
            extendsFrom testImplementation
        }
        integrationTestCompileOnly {
            extendsFrom testCompileOnly
        }
        integrationTestRuntimeOnly {
            extendsFrom testRuntimeOnly
        }
    }

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
            // Mark integration test folders as test code in IntelliJ
            // https://youtrack.jetbrains.com/issue/IDEA-151925#comment=27-2355076
            testSourceDirs += project.sourceSets.integrationTest.java.srcDirs
            testSourceDirs += project.sourceSets.integrationTest.resources.srcDirs
        }
    }

    dependencyManagement {
        dependencies {
            dependency 'org.junit.jupiter:junit-jupiter-api:5.7.0'
            dependency 'org.junit.jupiter:junit-jupiter-engine:5.7.0'

            dependency 'javax.inject:javax.inject:1'
            dependency 'javax.ws.rs:javax.ws.rs-api:2.1.1'

            dependency "org.springframework.boot:spring-boot:${springBootVersion}"
            dependency "org.springframework.boot:spring-boot-starter:${springBootVersion}"
            dependency "org.springframework.data:spring-data-cassandra:3.3.0"
            dependency "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
            dependency "org.springframework.boot:spring-boot-starter-jetty:${springBootVersion}"
            dependency "org.springframework.boot:spring-boot-starter-jersey:${springBootVersion}"

            dependency "org.glassfish.jersey.core:jersey-server:${jerseyVersion}"
            dependency "org.glassfish.jersey.core:jersey-client:${jerseyVersion}"
            dependency "org.glassfish.jersey.core:jersey-common:${jerseyVersion}"
            dependency "org.glassfish.jersey.ext:jersey-proxy-client:${jerseyVersion}"
            dependency "org.glassfish.jersey.ext:jersey-spring5:${jerseyVersion}"
            dependency "org.glassfish.jersey.media:jersey-media-jaxb:${jerseyVersion}"
            dependency "org.glassfish.jersey.containers:jersey-container-servlet-core:${jerseyVersion}"
            dependency "org.glassfish.jersey.containers:jersey-container-servlet:${jerseyVersion}"
            dependency "org.glassfish.jersey.inject:jersey-hk2:${jerseyVersion}"

            dependency 'com.google.guava:guava:31.0.1-jre'
            dependency 'org.apache.commons:commons-lang3:3.12.0'

            // Jackson
            dependency "com.fasterxml.jackson.core:jackson-databind:${ext['jackson.version']}"
            dependency "com.fasterxml.jackson.core:jackson-annotations:${ext['jackson.version']}"
            dependency "com.fasterxml.jackson.core:jackson-core:${ext['jackson.version']}"
            dependency "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${ext['jackson.version']}"
            dependency "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${ext['jackson.version']}"
            dependency "com.fasterxml.jackson.datatype:jackson-datatype-guava:${ext['jackson.version']}"
            dependency "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${ext['jackson.version']}"
        }
    }

    test {
        useJUnitPlatform()
    }
}